#version 460 core

layout(local_size_x = 128, local_size_y = 1, local_size_z = 1) in;

struct Particle {
    vec3 position;
    float density;
    vec3 velocity;
    float pressure;
};

layout(std430, binding = 0) restrict buffer Particles {
    Particle particles[];
};

layout(std430, binding = 1) restrict readonly buffer Counts {
    uint counts[];
};

layout(std430, binding = 2) restrict readonly buffer Offsets {
    uint offsets[];
};

layout(std430, binding = 3) restrict buffer Debug {
    uint debug[];
};

uniform float size;
uniform float binSize;
uniform int gridRes;
uniform int numParticles;
uniform float particleMass;
uniform float kernelRadius;
uniform float stiffness;
uniform float restDensity;
uniform float restPressure;

// neighborhood coordinate offsets
const ivec3 NEIGHBORHOOD[27] = {
    ivec3(-1, -1, -1), ivec3(-1, -1,  0), ivec3(-1, -1,  1),
    ivec3(-1,  0, -1), ivec3(-1,  0,  0), ivec3(-1,  0,  1),
    ivec3(-1,  1, -1), ivec3(-1,  1,  0), ivec3(-1,  1,  1),
    ivec3( 0, -1, -1), ivec3( 0, -1,  0), ivec3( 0, -1,  1),
    ivec3( 0,  0, -1), ivec3( 0,  0,  0), ivec3( 0,  0,  1),
    ivec3( 0,  1, -1), ivec3( 0,  1,  0), ivec3( 0,  1,  1),
    ivec3( 1, -1, -1), ivec3( 1, -1,  0), ivec3( 1, -1,  1),
    ivec3( 1,  0, -1), ivec3( 1,  0,  0), ivec3( 1,  0,  1),
    ivec3( 1,  1, -1), ivec3( 1,  1,  0), ivec3( 1,  1,  1)
};

// Equation (10) from Harada
float poly6Kernel(float r, float h) {
    return 315.0 / (64.0 * 3.14 * pow(h, 9)) * pow(h * h - r * r, 3);
}

float W(float r) {
    return poly6Kernel(r, kernelRadius);
}

float wallDensity(vec3 p) {
    float density = 0;

    if (p.x < kernelRadius) {
        density += particleMass * W(p.x);
    } else if (p.x > size - kernelRadius) {
        density += particleMass * W(size - p.x);
    }

    if (p.y < kernelRadius) {
        density += particleMass * W(p.y);
    } else if (p.y > size - kernelRadius) {
        density += particleMass * W(size - p.y);
    }

    if (p.z < kernelRadius) {
        density += particleMass * W(p.z);
    } else if (p.y > size - kernelRadius) {
        density += particleMass * W(size - p.z);
    }

    return density * 5;
}

void main() {
    const uint particleID = gl_GlobalInvocationID.x;
    if (particleID >= numParticles) {
        return;
    }

    Particle p = particles[particleID];
    const ivec3 coord = clamp(ivec3(p.position / binSize), ivec3(0), ivec3(gridRes - 1));

    float density = particleMass * W(0);
    uint d = 0;

    // search the particles of each neighboring bin
    #pragma unroll 1
    for (uint binIndex = 0; binIndex < 27; binIndex++) {
        const ivec3 nc = coord + NEIGHBORHOOD[binIndex];

        // don't go out of bounds
        if (any(lessThan(nc, ivec3(0))) || any(greaterThanEqual(nc, ivec3(gridRes)))) {
            continue;
        }

        const uint index = nc.z * gridRes * gridRes + nc.y * gridRes + nc.x;
        const uint count = counts[index];
        const uint offset = offsets[index];

        // search the particles of this bin
        for (uint localIndex = 0; localIndex < count; localIndex++) {
            const uint otherParticleID = offset + localIndex;
            if (particleID == otherParticleID) {
                continue;
            }

            // find the distance, ignore if too far
            const vec3 r = p.position - particles[otherParticleID].position;
            const float dist = length(r);
            if (dist >= kernelRadius) {
                continue;
            }

            // Equation (4) from Harada
            density += particleMass * W(dist);
        }
    }

    p.density = density + wallDensity(p.position);

    // Equation (5) from Harada
    p.pressure = restPressure + stiffness * (density - restDensity);
    // Alternative presure computation (Desbrun and Cani, 1996)
    const float pressureConst = 3;
    p.pressure = restPressure + stiffness * (pow(density / restDensity, pressureConst) - 1);

    particles[particleID] = p;
    debug[particleID] = d;
}
