#version 460 core

layout(local_size_x = 128, local_size_y = 1, local_size_z = 1) in;

struct Particle {
    vec3 position;
    float density;
    vec3 velocity;
    float pressure;
};

layout(std430, binding = 0) restrict buffer Particles {
    Particle particles[];
};

layout(std430, binding = 1) restrict readonly buffer Counts {
    uint counts[];
};

layout(std430, binding = 2) restrict readonly buffer Offsets {
    uint offsets[];
};

layout(std430, binding = 2) restrict readonly buffer Sorted {
    uint sorted[];
};

uniform float size;
uniform float binSize;
uniform int gridRes;
uniform float dt;
uniform int numParticles;
uniform vec3 gravity;
uniform float particleMass;
uniform float kernelRadius;
uniform float viscosityCoefficient;
uniform float viscosityWeight;
uniform float pressureWeight;
uniform float particleRadius;

// neighborhood coordinate offsets
const ivec3 NEIGHBORHOOD[27] = {
    ivec3(-1, -1, -1), ivec3(-1, -1,  0), ivec3(-1, -1,  1),
    ivec3(-1,  0, -1), ivec3(-1,  0,  0), ivec3(-1,  0,  1),
    ivec3(-1,  1, -1), ivec3(-1,  1,  0), ivec3(-1,  1,  1),
    ivec3( 0, -1, -1), ivec3( 0, -1,  0), ivec3( 0, -1,  1),
    ivec3( 0,  0, -1), ivec3( 0,  0,  0), ivec3( 0,  0,  1),
    ivec3( 0,  1, -1), ivec3( 0,  1,  0), ivec3( 0,  1,  1),
    ivec3( 1, -1, -1), ivec3( 1, -1,  0), ivec3( 1, -1,  1),
    ivec3( 1,  0, -1), ivec3( 1,  0,  0), ivec3( 1,  0,  1),
    ivec3( 1,  1, -1), ivec3( 1,  1,  0), ivec3( 1,  1,  1)
};

// Equation (8) from Harada
vec3 Wpress(vec3 r, float d) {    
    const float weight = -45.0 / (3.14 * pow(kernelRadius, 6));
    return pow(kernelRadius - d, 2) * (r / d) * weight;
}

// Equation (9) from Harada
float Wvis(float r) {
    const float weight = 45.0 / (3.14 * pow(kernelRadius, 6));
    return (kernelRadius - r) * weight;
}

void main() {
    const uint particleID = gl_GlobalInvocationID.x;
    if (particleID >= numParticles) {
        return;
    }

    Particle p = particles[particleID];
    const ivec3 coord = ivec3(p.position / binSize);
    
    vec3 pressureForce = vec3(0);
    vec3 viscosityForce = vec3(0);
    vec3 externalForces = gravity * p.density;

    // search the particles of each neighboring bin
    // #pragma unroll 1
    for (uint binIndex = 0; binIndex < 27; binIndex++) {
        const ivec3 nc = coord + NEIGHBORHOOD[binIndex];

        // don't go out of bounds
        if (any(lessThan(nc, ivec3(0))) || any(greaterThanEqual(nc, ivec3(gridRes)))) {
            continue;
        }

        const uint index = nc.z * gridRes * gridRes + nc.y * gridRes + nc.z;
        const uint count = counts[index];
        const uint offset = offsets[index];

        // search the particles of this bin
        for (uint localIndex = 0; localIndex < count; localIndex++) {
            const uint otherParticleID = offset + localIndex;
            if (particleID == otherParticleID) {
                continue;
            }

            Particle other = particles[otherParticleID];

            // find the distance, ignore if too far
            const vec3 r = p.position - other.position;
            const float dist = length(r);
            if (dist >= kernelRadius) {
                continue;
            }

            // Equation (6) from Harada
            const float pressure = (p.pressure + other.pressure) / (2.0 * other.density);
            // if (pressure > 0) {
            // calculate pressure weight as a vector
            vec3 mPressureWeight = Wpress(r, dist + 1e-16);
            pressureForce -= particleMass * pressure * mPressureWeight;
            // }
    
            // Equation (7) from Harada
            // const vec3 velocityDiff = other.velocity - p.velocity;
            // viscosityForce += particleMass * (velocityDiff / other.density) * Wvis(dist);
        }
    }

    // Sum of Equations (6) and (7) and external forces from Herada
    viscosityForce *= viscosityCoefficient;
    const vec3 force = pressureForce + viscosityForce + externalForces;

    // find accelaration and integrate for next velocity
    const vec3 acceleration = force / (p.density + 1e-16);
    vec3 vel = p.velocity + acceleration * dt;
    vec3 pos = p.position + vel * dt;
    
    const float wallDamping = 0.3;
    const float border = 0.01;

    if (pos.x < border) {
        vel.x *= -wallDamping;
        pos.x = border;
    } else if (pos.x > size - border) {
        vel.x *= -wallDamping;
        pos.x = size - border;
    }

    if (pos.y < border) {
        vel.y *= -wallDamping;
        pos.y = border;
    } else if (pos.y > size - border) {
        vel.y *= -wallDamping;
        pos.y = size - border;
    }

    if (pos.z < border) {
        vel.z *= -wallDamping;
        pos.z = border;
    } else if (pos.z > size - border) {
        vel.z *= -wallDamping;
        pos.z = size - border;
    }

    p.velocity = vel;
    p.position = pos;
    particles[particleID] = p;
}
