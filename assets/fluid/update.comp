#version 460 core
#extension GL_ARB_compute_shader : enable
#extension GL_ARB_shader_storage_buffer_object : enable

layout(local_size_x = 128, local_size_y = 1, local_size_z = 1) in;

layout(location = 0, r32ui) uniform restrict uimage3D countGrid;
layout(location = 1, r32ui) uniform restrict uimage3D offsetGrid;
layout(location = 2) uniform sampler3D velocityField;
uniform float size;
uniform float binSize;
uniform int gridRes;
uniform float dt;
uniform int numParticles;
uniform vec3 gravity;
uniform float particleMass;
uniform float kernelRadius;
uniform float viscosityCoefficient;
uniform float viscosityWeight;
uniform float pressureWeight;

struct Particle {
    vec3 position;
    vec3 velocity;
    float density;
    float pressure;
};

layout(std430, binding = 1) restrict buffer Particles {
    Particle particles[];
};

const ivec3 NEIGHBORHOOD[27] = {
    ivec3(-1, -1, -1), ivec3(0, -1, -1), ivec3(1, -1, -1),
    ivec3(-1, -1,  0), ivec3(0, -1,  0), ivec3(1, -1,  0),
    ivec3(-1, -1,  1), ivec3(0, -1,  1), ivec3(1, -1,  1),
    ivec3(-1,  0, -1), ivec3(0,  0, -1), ivec3(1,  0, -1),
    ivec3(-1,  0,  0), ivec3(0,  0,  0), ivec3(1,  0,  0),
    ivec3(-1,  0,  1), ivec3(0,  0,  1), ivec3(1,  0,  1),
    ivec3(-1,  1, -1), ivec3(0,  1, -1), ivec3(1,  1, -1),
    ivec3(-1,  1,  0), ivec3(0,  1,  0), ivec3(1,  1,  0),
    ivec3(-1,  1,  1), ivec3(0,  1,  1), ivec3(1,  1,  1)
};

void main() {
    const uint particleID = gl_GlobalInvocationID.x;
    if (particleID >= numParticles) {
        return;
    }

    Particle p = particles[particleID];
    const ivec3 binCoord = ivec3(p.position / binSize);
    
    vec3 forcePressure = vec3(0);
    vec3 forceViscosity = vec3(0);

    #pragma unroll 1
    for (uint binIndex = 0; binIndex < 27; binIndex++) {
        const ivec3 neighborCoord = binCoord + NEIGHBORHOOD[binIndex];

        if (
            any(lessThan(neighborCoord, ivec3(0))) || 
            any(greaterThanEqual(neighborCoord, ivec3(gridRes)))
        ) {
            continue;
        }

        const uint count = imageLoad(countGrid, neighborCoord).x;
        const uint offset = imageLoad(offsetGrid, neighborCoord).x;

        for (uint localIndex = 0; localIndex < count; localIndex++) {
            const uint otherParticleID = offset + localIndex; 
            Particle other = particles[otherParticleID];
            const vec3 r = p.position - other.position;
            const float len = length(r);

            // ignore if out of bounds
            if (len >= kernelRadius) {
                continue;
            }

            vec3 mPressureWeight = vec3(0);

            if (len > 0) {
                mPressureWeight = pressureWeight * pow(kernelRadius - len, 3) * (r / len);
            }

            const float appliedPressure = p.pressure + other.pressure;
            forcePressure += (particleMass * appliedPressure * mPressureWeight) / (2.0 * other.density);

            const float mViscosityWeight = viscosityWeight * (kernelRadius - len);
            const vec3 filteredVelocity = texture(velocityField, other.position / size).xyz;
            const vec3 velocityDiff = filteredVelocity - other.velocity;
            forceViscosity += (particleMass * velocityDiff * mViscosityWeight) / other.density;
        }
    }

    const vec3 forceGravity = gravity * p.density;
    const vec3 force = (forceViscosity * viscosityCoefficient) - forcePressure + forceGravity;
    const vec3 acceleration = force / p.density;

    vec3 vel = p.velocity + acceleration;
    vec3 pos = p.position + vel * dt;
    const float wallDamping = 0.5;

    if (pos.x < 0) {
        vel.x *= -wallDamping;
        pos.x = 0;
    } else if (pos.x > size) {
        vel.x *= -wallDamping;
        pos.x = size;
    }

    if (pos.y < 0) {
        vel.y *= -wallDamping;
        pos.y = 0;
    } else if (pos.y > size) {
        vel.y *= -wallDamping;
        pos.y = size;
    }

    if (pos.z < 0) {
        vel.z *= -wallDamping;
        pos.z = 0;
    } else if (pos.z > size) {
        vel.z *= -wallDamping;
        pos.z = size;
    }

    p.velocity = vel;
    p.position = pos;
    particles[particleID] = p;
}
