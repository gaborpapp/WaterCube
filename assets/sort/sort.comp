#version 460 core
#extension GL_ARB_compute_shader : enable
#extension GL_ARB_shader_storage_buffer_object : enable

layout(local_size_x = 128, local_size_y = 1, local_size_z = 1) in;

struct Particle {
    vec3 position;
    vec3 velocity;
    float density;
    float pressure;
};

layout(std430, binding = 0) buffer Particles {
    Particle particles[];
};

layout(binding = 1, r32ui) uniform restrict uimage3D countGrid;
layout(binding = 2, r32ui) uniform restrict uimage1D sortedIds;
layout(binding = 3, r32ui) uniform restrict readonly uimage3D offsetGrid;
uniform float binSize;
uniform int numItems;

void main() {
    const uint particleID = gl_GlobalInvocationID.x;
    if (particleID >= numItems) {
        return;
    }

    const Particle p = particles[particleID];
    const ivec3 binCoord = ivec3(p.position / binSize);
    const uint globalOffset = imageLoad(offsetGrid, binCoord).x;
    const uint localOffset = imageAtomicAdd(countGrid, binCoord, 1);
    const int globalIndex = int(globalOffset) + int(localOffset);
    imageStore(sortedIds, globalIndex, ivec4(particleID));
}
